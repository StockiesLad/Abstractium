//file:noinspection GroovyAssignabilityCheck
//file:noinspection GrUnresolvedAccess
plugins {
	id 'java'
	id 'idea'
	id 'eclipse'
	id 'fabric-loom' version '1.3.+'
	id "com.modrinth.minotaur" version "2.+"
	id "com.matthewprenger.cursegradle" version "1.+"
}

loom {
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.minecraft_version}+build.${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"

	implementation"it.unimi.dsi:fastutil:${project.fastutil_version}"
	modImplementation(include 'com.github.Fallen-Breath:conditional-mixin:v0.3.2')

	afterEvaluate {
		subprojects.each {
			//noinspection ForeignDelegate
			include project("${it.name}:")
		}
	}

}

allprojects {
	apply plugin: 'java'
	apply plugin: 'idea'
	apply plugin: 'eclipse'
	apply plugin: 'fabric-loom'
	apply plugin: 'maven-publish'

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17

	archivesBaseName = project.archives_base_name
	version = project.mod_version
	group = project.maven_group

	sourceSets {
		testmod {
			compileClasspath += main.compileClasspath
			runtimeClasspath += main.runtimeClasspath
		}
	}

	repositories {
		maven { url = "https://maven.terraformersmc.com/" }
		maven { url = "https://maven.fabricmc.net/" }
		maven { url 'https://jitpack.io' }
	}

	configurations {
		dev {
			canBeResolved = false
			canBeConsumed = true
		}
	}

	processResources {
		Map<String, String> properties = new HashMap<>()
		//noinspection GroovyAssignabilityCheck
		properties.put("version", version)
		properties.put("modid", modid)
		properties.put("mod_name", mod_name)

		properties.put("fapi_1182", fapi_1182)


		properties.forEach((k, v) -> inputs.property(k, v))

		filesMatching("fabric.mod.json") {
			expand properties
		}
	}

	tasks.withType(JavaCompile).configureEach {
		//noinspection GroovyAssignabilityCheck
		//noinspection GroovyAccessibility
		it.options.release = 17
	}

	java {
		withSourcesJar()
	}

	jar {
		from("LICENSE") {
			rename { "${it}_${project.archivesBaseName}" }
		}
	}

	publishing {
		publications {
			//noinspection GroovyAssignabilityCheck
			mavenJava(MavenPublication) {
				from components.java
			}
		}

		repositories {
		}
	}
}

apply from: "gradle/publishing/publishing.gradle"


subprojects {
	dependencies {
		modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
		implementation project(path: ':', configuration: "namedElements")
		modImplementation 'com.github.Fallen-Breath:conditional-mixin:v0.3.2'
	}

	version = rootProject.version
}
